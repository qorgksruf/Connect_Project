plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.10'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

// --------- 그레이들 한글 인코딩
compileJava.options.encoding = 'UTF-8'
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
// --------- 그레이들 한글 인코딩

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'


configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web' // 스프링 부트
    testImplementation 'org.springframework.boot:spring-boot-starter-test'  // 스프링 부트 테스터
    compileOnly 'org.projectlombok:lombok'  // 롬복
    annotationProcessor 'org.projectlombok:lombok'  // 롬복
    runtimeOnly 'com.mysql:mysql-connector-j' // MySql
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa' // JPA
    implementation 'org.springframework.boot:spring-boot-starter-websocket' //웹소켓
    implementation 'commons-io:commons-io:2.11.0' // 이미지 클라이언트 전송
}

tasks.named('test') {
    useJUnitPlatform()
}

/* 스프링부트 빌드시 리액트도 같이 빌드  */
def frontendDir = "$projectDir/src/main/frontend" // 리액트 프로젝트 폴더 경로
// def frontendDir = "$projectDir/src/main/{리액트프로젝트명}"

sourceSets { // 리액트 빌드 후 빌드된 파일을 저장할 경로
    main {
        // /src/main/resources 스프링이 view 매핑하는 기본 경로이기 때문에
        resources { srcDirs = ["$projectDir/src/main/resources"]
        }
    }
}
// 빌드된 리액트 파일을 복사해서 위에 설정한 경로에 저장
processResources { dependsOn "copyReactBuildFiles" }

// 리액트 npm 설치
task installReact(type: Exec) { // 리액트 [ npm ] 없으면 자동 설치 , 있으면 업데이트
    workingDir "$frontendDir"
    inputs.dir "$frontendDir"
    group = BasePlugin.BUILD_GROUP
    if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
        commandLine "npm.cmd", "audit", "fix"
        commandLine 'npm.cmd', 'install' }
    else {
        commandLine "npm", "audit", "fix" commandLine 'npm', 'install'
    }
}

task buildReact(type: Exec) { // 리액트 빌드 [ 실행 ] npm start
    dependsOn "installReact"
    workingDir "$frontendDir"
    inputs.dir "$frontendDir"
    group = BasePlugin.BUILD_GROUP
    if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
        commandLine "npm.cmd", "run-script", "build"
    } else {
        commandLine "npm", "run-script", "build"
    }
}

task copyReactBuildFiles(type: Copy) { // 리액트 빌드후 파일을 스프링 [ resource ] 로 빌드파일 저장
    dependsOn "buildReact"
    from "$frontendDir/build"
    into "$projectDir/src/main/resources/static"
}


